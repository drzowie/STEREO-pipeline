=head2 merge_batches - merge a dataset from the hi-2 pipeline

=for usage 

 merge_batches( \@files, \%opt );

=for ref

You feed in a collection of file names and an optional options hash,
and the files are merged into a single set of FITS files in the
destination directory ("L2" by default or set by the option
"MERGED_DIR").

The batch files are averaged together wherever there are valid values
in two or more batch files; near the transition a given batch is ramped
up smoothly via Hanning window (by default, this smoothing region is 40
pixels wide; you can set it with the "MARGIN" option).

Options processed by merge_batches are:

=over 3

=item MERGED_DIR (default = "L2")

The directory in which to place the merged files

=item PROCESSES (default = 8)

How many separate processes to run, one file per process

=item MARGIN (default=40)

This is the size of the Hanning margin in which the weight of a given
batch file is rolled to zero.  It smoothes the boundaries between batches
in the output data.

=item START (default=0)

This is the index of the first file to write, in the array of potential output files.

=item REGEXP (default='clean\-(.*)\-\-BATCH.*')

This is a regexp that input file names must match, to be detected for merging.

=item PTYPE (default='')

If present, this should be a single character (e.g. 'S' or 'M') indicating the 
type of L2 processing that was performed on the files being merged.  It's used for
HI-1 pipeline processing.

=item BAD_THRESH (default=1000)

Pixels with an absolute value larger than this are treated as BAD values and ignored.

=item USE_HDR_FNAME (default=1)

If the field FILENAME is present in the headers of processed files, use it.
This is a switch in case of difficulty with the header processing of the various
pipelines: you can turn it off if need be.

=back

=cut

use strict;
use Date::Format;

sub merge_batches {
    my $files = shift;
    my $opt = shift;
    my $VERSION = "2.3 1-Mar-2014";
    
    $opt = {} unless defined($opt);
    $opt->{MERGED_DIR} = "L2" unless(defined $opt->{MERGED_DIR});
    $opt->{PROCESSES} = 8 unless(defined $opt->{PROCESSES});
    $opt->{MARGIN} = 40 unless(defined $opt->{MARGIN});    
    $opt->{START} = 0 unless(defined $opt->{START});
    $opt->{REGEXP} = 'clean\-(.*)\-\-BATCH.*' unless(defined $opt->{REGEXP});
    $opt->{PTYPE} = '' unless(defined($opt->{PTYPE}));  # processing type -- 'S' or 'M'
    $opt->{BAD_THRESH} = 1e3 unless(defined($opt->{BAD_THRESH}));

    my $ptype = $opt->{PTYPE};

    if($opt->{MERGED_DIR}  and  !(-d $opt->{MERGED_DIR})){
	`mkdir $opt->{MERGED_DIR}`;
    }

    my %filenames = ();
    
    print "Sorting ".(0+@$files)." files based on name...\n";

    for $file(@$files) {
	my $f2 = $file;
	$f2 =~ s:.*/::;
	unless($f2 =~ m/$opt->{REGEXP}/) {
	    print "File $f2 doesn't match -- skipping\n";
	    next;
	} 
	my $fdate = $1;
	print "fdate=$fdate\n";
	unless(exists($filenames{$fdate})){
	    $filenames{$fdate} = [];
	}
	
	push(@{$filenames{$fdate}}, $file);
    }

    # Now the filenames have been sorted.  Begin accumulating.
    # This is slightly complicated by the fact that we run several accumulations in parallel.  The 
    # main loop spawns out several daughter processes, each of which writes one file.
    # It also maintains a certain number of active processes, reaping their statuses as they expire.

    my %pids = ();

    my @k = sort keys %filenames;
    print "start is $opt->{START}; k is $#k\n";
    for my $ii($opt->{START}..$#k) {

	my $f = $k[$ii];

	next unless(@{$filenames{$f}} > 1);

    	while( (0 + keys %pids) >= $opt->{PROCESSES} ) {
	  my $pid = wait();
	  if($pid<0) {
	      %pids = ();
	  }
	  delete $pids{$pid};
	}

	if((0 + keys %pids) < $opt->{PROCESSES}) {

	    my $fh = rfits($filenames{$f}->[0],{DATA=>0});
	    my $out_fname;
	    if($opt->{USE_HDR_FNAME} && $fh->{FILENAME}) {
		$out_fname = $fh->{FILENAME};
		print "f is $f; \$filenames{\$f}->[0] is $filenames{$f}->[0]; fh->{FILENAME} is $fh->{FILENAME}\n";
		$out_fname =~ s/_[1b](....\.fts)$/_2$ptype$1/;  # update L1 to L2
	    } else {
		$out_fname = "merged-$f.fits";
	    }
	    my $mfname = sprintf("%s/%s",($opt->{MERGED_DIR} || "."),$out_fname) ;

	    my $pid = fork();

	    if($pid<0) {
		die "fork failed\n";
	    } 

	    if($pid) {
		# parent
		$pids{$pid}=$mfname;
		print "Spawned task for $mfname\n";
	    } else {
		# daughter
		print "Evalling...\n";
		eval {
		    my $ksiz = $opt->{MARGIN}*2 + 1;
		    my $k = (1 + cos( (rvals($ksiz,$ksiz)*3.141592653/$opt->{MARGIN})->clip(0,)));
		    $k /= $k->sum;
		    
		    print "$f: ".(0+@{$filenames{$f}})." files...";

		    my @cube = mrfits(@{$filenames{$f}});
		    my $cube = pdl(@cube)->mv(-1,0)->sever;
		    my $hdr = $cube[0]->gethdr;
		    my $ncube = 0+@cube;
		    
		    my $baddies = !($cube->isfinite->short & ($cube->abs < $opt->{BAD_THRESH}) );  # actual baddies
		    $cube->where($baddies) .= 0;  	      # Mask out the bad values in the original cube so they don't cause trouble later

		    my $badmask = $baddies->copy;             # becomes a dilated mask

		    # dilate the badmask
		    for my $i(0..$badmask->dim(0)-1) {
			print ",";
			$badmask->(($i)) .= $badmask->(($i))->convolveND(ones($opt->{MARGIN}*2+1,1),{method=>'direct'});
			$badmask->(($i)) .= $badmask->(($i))->convolveND(ones(1,$opt->{MARGIN}*2+1),{method=>'direct'});
		    }

		    # Generate the feathering screen.  Start with 1 for good, 0 for bad.
		    my $weight = ($badmask==0)->float;

		    # Convolve the weight for smooth transition
		    for my $i(0..$weight->dim(0)-1) {
		    	print ".";
			$weight->(($i)) .= $weight->(($i))->convolveND($k);
		    }
		    $weight->where($weight < 1e-3) .= 0;

		    my $avg = ($cube*$weight)->sumover / $weight->sumover;
		    
		    ##############################
		    # Add new header fields
		    $hdr->{HISTORY} .= "Merged $ncube batches with merge_batch v$VERSION\n";
		    $hdr->{ORIGIN}  = 'SwRI (contact: deforest@boulder.swri.edu) / Post-processed at SwRI';

		    $hdr->{DATE} = time2str("%Y-%m-%dT%H:%M:%S",time,'Z');
		    my $ptype = $opt->{PTYPE};
		    $hdr->{FILENAME} = $out_fname;
		    $avg = float($avg);
		    $avg->sethdr($hdr);
		    print "writing $mfname\n";
		    wfits( $avg, $mfname);
		};
		if($@){
			print STDERR "daughter: $@\n";
		}
		exit 0;
	    }
	} else {
	    print "Hmmm...\n";
	}
    }
    while(keys %pids) {
	my $p = wait();
	return unless(defined($p) and $p>0);
	delete $pids{$p};
    }
}    
