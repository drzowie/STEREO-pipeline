#-*-perl-*-
use strict;
=head2 shift_to_instrument - part of the hi2 pipeline.

=for ref

Shifts the cleaned images (that are in celestial coordinates, in
C<CEL_CUBE>) back to observing coordinates, in C<CLEAN_CUBE>.

C<shift_to_instrument> uses C<PDL::Transform> and the distortion
function stored in the C<D_PARAMS> field.  It takes only one option,
C<INTERP_METHOD>, which should match what was used by
C<shift_to_celestial>.

=cut

sub shift_to_instrument {
    my $hash = shift;
    my $opt = shift;
    $VERSION = "2.1 24-May-2013";

    $opt = {} unless(defined $opt);

    $opt->{'INTERP_METHOD'} = 'j' unless(defined $opt->{'INTERP_METHOD'}); 

    my $i;

    my $xr = $hash->{'D_PARAMS'}->{'xr'};
    my $yr = $hash->{'D_PARAMS'}->{'yr'};

    my $dex0 = int($hash->{UTIME}->dim(0)/2);
    my $t0 = $hash->{UTIME}->(($dex0));
    my $h0 = $hash->{RAW_HDRS}->[$dex0];
    
    our $t_nl = t_nl(@{$hash->{'D_PARAMS'}->{'tnl_params'}});
    
    $hash->{'CLEAN_CUBE'}= PDL->new_from_specification(float, 
						       $hash->{RAW_HDRS}->[0]->{NAXIS1},
						       $hash->{RAW_HDRS}->[0]->{NAXIS2},
						       0+@{$hash->{RAW_HDRS}});
    $hash->{'CLEAN_CUBE'}->badflag(1);
    
    for $i(0..$hash->{'CEL_CUBE'}->dim(2)-1) {
	print "Resampling image $i of ".$hash->{'CEL_CUBE'}->dim(2)." back to observing coordinates...\n";
	
	my $delta_t =  ($t0 - $hash->{'UTIME'}->(($i)) ) / 3600 /24;
	my $orbital_rotation = $hash->{'D_PARAMS'}->{'ospeed'} * $delta_t;
	
	print "Shifting back by $delta_t days (".(-$orbital_rotation)." degrees)..\n";
	my $t_o = t_linear(pre=> [ $orbital_rotation, 0]);
	
        my $t_crop = t_linear(pre=>[ - $xr->at(0) , -$yr->at(0) ]);

	print "i=$i\n";
	my $t =	!$t_nl x !$t_o x $t_nl x !$t_crop;
	my $siz = [$hash->{CLEAN_CUBE}->dim(0),$hash->{CLEAN_CUBE}->dim(1)];

	# Make a dilated BAD mask (dilated by 1 pixel).
	print "m.";
	$mask = (($hash->{'CEL_CUBE'}->(:,:,($i))->isbad->float)->convolveND(ones(3,3)) > 0)->byte;

	# Remap the BAD mask using sampling
	print "mm.";
	$ipmask = $mask->map(
	    $t, 
	    $siz,
	    {method=>'s', pix=>1});

	print "data";
	# Remap the data and mark it BAD if necessary.
	$hash->{'CLEAN_CUBE'}->(:,:,($i)) .= 
	    $hash->{'CEL_CUBE'}->(:,:,($i))->map( 
		$t, 
		$siz, 
		{method=>$opt->{'INTERP_METHOD'}, pix=>1}
	    )->setbadif($ipmask);
	print " ";
    }
    print "\n";
    push(@{$hash->{log}},"  shift_to_instrument v$VERSION");
    print "shift_to_instrument returning\n";
    return $hash;
}
